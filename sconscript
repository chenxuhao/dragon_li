#! /bin/python

import os
Import('env')

cppdefines = []
cpppath = [
	'.',
	'/usr/local/cuda/include'
	]
cxx = ['g++']
cxxflags = [
	'-std=c++0x'
	]
libpath = [
	'/usr/local/cuda/lib64'
	]

env.Replace(
	CC			 = cxx,
    CPPDEFINES   = cppdefines,    # pre-processor defines (e.g. -DFOO)
    CPPPATH      = cpppath,       # pre-processor paths   (e.g. -Ifoo/bar)
    CXX          = cxx,           # c++ compiler
    CXXFLAGS     = cxxflags,      # compiler flags (not pre-processor or linker)
    LIBPATH      = libpath       # compile-time library paths (e.g. -L)
)


hydrazine_src = []
hydrazine_dirs = [
	'hydrazine/implementation',
	'hydrazine/interface'
	]
exts = ['*.cpp']
for dirname in hydrazine_dirs:
	for extname in exts:
		src = Glob(os.path.join(dirname, extname))
		hydrazine_src.extend(src)

hydrazine_lib = env.StaticLibrary('libhydrazine', hydrazine_src, LIBS=['-lrt'])

NVCC = '/usr/local/cuda/bin/nvcc'
NVCCFLAGS = '-arch=sm_20 -m64'
NVCCPATH = ['.']

env.Append(BUILDERS = {'Cuda' : Builder(
	action = NVCC + ' -I. ' + NVCCFLAGS + ' $SOURCE -c -o $TARGET',
	suffix = '.o',
	src_suffix = '.cu'
)})

dragon_li_src = []
dragon_li_dirs = ['tests']
dragon_li_install = env['install_path']
exts = ['*.cu']
for dirname in dragon_li_dirs:
	for extname in exts:
		src = env.Glob(os.path.join(dirname, extname))
		dragon_li_src.extend(src)

for app in dragon_li_src:
	appfile = os.path.split(str(app))[1]
	appname = os.path.splitext(appfile)[0]
	mappedApp = env.Cuda(app)
	buildApp = env.Program(os.path.join('bin', appname), mappedApp, LIBS = [hydrazine_lib, '-lcudart'])
	env.Install(dragon_li_install, [buildApp])

